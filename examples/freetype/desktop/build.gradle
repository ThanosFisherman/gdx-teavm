project.ext.mainClassName = "com.github.xpenatan.gdx.examples.desktop.Main"

project.ext.assetsDir = new File("/assets");

dependencies {
    implementation "com.badlogicgames.gdx:gdx-platform:$project.gdxVersion:natives-desktop"
    implementation "com.badlogicgames.gdx:gdx-backend-lwjgl:$project.gdxVersion"
    implementation "com.badlogicgames.gdx:gdx-box2d-platform:$project.gdxVersion:natives-desktop"
    implementation "com.badlogicgames.gdx:gdx-freetype-platform:$project.gdxVersion:natives-desktop"
    implementation project(":examples:freetype:core")
}

tasks.register('runFreetypeDesktop', JavaExec) {
    dependsOn classes
    setGroup("examples-desktop")
    setDescription("Run Freetype example")
    mainClass.set(project.mainClassName)
    setClasspath(sourceSets.main.runtimeClasspath)
    workingDir = project.assetsDir

    if (org.gradle.internal.os.OperatingSystem.current() == org.gradle.internal.os.OperatingSystem.MAC_OS) {
        // Required to run on macOS
        jvmArgs += "-XstartOnFirstThread"
    }
}

tasks.register('dist', Jar) {
    manifest {
        attributes 'Main-Class': project.mainClassName
    }
    dependsOn configurations.runtimeClasspath
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    with jar
}